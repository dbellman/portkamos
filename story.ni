"Port Kamos" by Dr Fax.

The story genre is "Horror". The story headline is "A story too close to home,". The release number is 1. The story creation year is 2015.
When play begins: 
say "You squeeze your eyes open against every force in the universe. You are seeing light and feeling the sharp agony of life for the very first time. You enter this world stunned.

Slowly shapes appear in front of you; from the centre first, then radially to the edge of your vision, slowly filling in the white void that seemed endless only seconds ago. As your surroundings become clearer, your confusion refuses to fade.

All of a sudden, the realisation hits you like a sack of dead batteries. Then moments later, a sack of dead batteries hits you like a sack of dead batteries.

You are definitely hungover, and probably assumed absent for your meeting.

That was the whole reason you came to this stupid town. You heard that there might be some chance of getting your foot in the door with the CEO of yet another mining company so you packed your bags and traveled all this way for- wait a second... where are your bags? In fact, where are you?

Your clothes smell like cigarette smoke and there is vomit on your breath. The height of the sun peeking through the window suggests that it is late morning, and this hypothesis is confirmed by a sudden overwhelming hunger.  Considering the meeting was suppose to be at 9:00AM, and you seem to have lost your phone and wallet as well, there isn't a great deal you can do. At least you are still wearing your trenchcoat.

You can't help but wonder what happened last night..."

Include Locksmith by Emily Short.
Include Rideable Vehicles by Graham Nelson. 
Include Basic Screen Effects by Emily Short. 

The carrying capacity of the player is 3.

[This is all comments and will not appear in the final version

PLOT
- wake up at kezzas
- escape zombies towards phentons house
- take refuge with fenton (he has a chemical which keeps the zombies away and is working on a cure)
- go talk to sheen
- tell sheen that phenton is working on a cure
- sheen leaves after he tells you some stuff
- go exploring
- find out phenton has been killed
- tell kezza that phenton is dead
- kezza says that maybe sheen is dead too!
- faux says that nothing is as it seems, which is obvious, or so it would seem.
- find way to get down to delapedated shack where crazy man is sitting with shotgun (hermit)
- he has sheen locked up and we think he is a baddie.
- turns out he knew sheen a long time ago and used to be part of mash. He clued onto sheen being evil.
- Sheen killed phenton but is wanting to help kill the monsters now
- the cure turns out to be a spectrum of light emitted from a particular molecule in the virus
- need to generate the molecule with secret ingredient (honeysuckle) then put solution infront of lighthouse light after bringing back electricity
- virus destroyed in surviving zombies and mankind is restored

Side tasks to get to the end:
1) - Faux turned off power in substation, must turn back on to get power to town. The sub station is next to the primary school [NYI].

2) - Must find secret ingredient of phentons substance (Honeysuckle). If you drink it you turn into one of them.

3) - Must give kezza the chainsaw so that he realizes he has to leave his spot. It was his previous belief that he could live life there only needing a reliable weapon to fend away any particularly aggressive monsters. He realizes that he shouldnt just wait it out and let someone else deal with it, but he should ball up and be part of the solution. Karlo throws the chainsaw down the cliff after you give it to him just to rub salt in the players wounds.


Other odds and ends:
a) - must enter karlos place by the mountain for the first time. When you see him he will be shocked and realize the hole in his defence. He will immediately put up barbs to stop things coming down that way. This forces the player to go past faux to get to and from kezza. Also, faux has the key to the asylum but you need to give him SOMETHING NOT YET DECIDED. MAYBE THE TOY GUN FROM SHEENS.

b) - the tide being high and low will vary during the game, this should stop you from getting between the beaches via the rocks.

c) - Should try and make the idea of the hermit man in the shack seem like he is dirty and evil to make people assume he is the enemy. Shopping complex/newsagent can have propaganda about murderer living in the shack or something. Also make sheen tell you he is evil (because sheen knows the hermit is onto him).


Places that need a use:
	
-Gnarles House
-Boat Ramp
-Shopping Region
	-Picnic Area
	-Desolate Shopping Complex
	-News Agency
	-The General Store
-Croseus Club
]

A floor is a kind of thing. Before putting something on a floor: try dropping the noun instead. A floor is always scenery. Instead of looking under a floor: say "Nice idea if you can figure out how. Seems kind of... logistically challenging." Understand "floor" or "ground" as a floor.

Some generic surroundings are backdrop. It is everywhere. Understand "walls" or "wall" or "ceiling" or "ground" or "area" or "room" or "here" as the generic surroundings. Understand "floor" or "floorboard" or "floorboards" as the generic surroundings when the location is not floored. Before putting something on the generic surroundings: try dropping the noun instead. Instead of examining the generic surroundings: say "You take another look around."; try looking. Instead of touching the generic surroundings: say "You encounter nothing extraordinary." Instead of touching the generic surroundings when in darkness: say "You try feeling your way around, but the space you are in is too large to allow you to navigate by touch."

Definition: a room is floored if it contains a floor.

Awakening is a scene. Awakening begins when play begins. Awakening ends when the player is in the Hidden Beach. Postulation is a scene. Postulation begins when Awakening ends.

Attacking it with is an action applying to one visible thing and one carried thing. Understand "attack [someone] with [something preferably held]" as attacking it with.

A room can be zombied. A room is usually not zombied.
A room can be threatening. A room is usually not threatening.

A zombie is a kind of animal. A zombie can be ignorant or aggressive. A zombie is usually aggressive. A zombie is usually scenery.

Instead of attacking someone:
	say "Not with your bare fists. You can hardly break a tissue with your hands."

A thing can be a weapon. A thing is usually not a weapon.

A person can be attackable. A person is usually not attackable.

Instead of attacking someone (called person) with something (called the tool):
	if the tool is not a weapon:
		say "That is not a weapon.";
	if the tool is a weapon:
		if person is attackable:
			say "You attack [person] with [the tool]. Fatally wounded, [person] scurries away to die someone more convenient for the author.";
			now the person is nowhere;
		if the person is not attackable:
			say "That would have significantly deleterious repercussions."
				
Use scoring. The maximum score is 100.

[__SCORE_______________________________________
2 points - finding knife	|
3 points - xing bushes	|
3 points - getting sheen to spill the beans	|
______________________________________________	|
Total:	|
8 points out of 100	|
_________________________________________________]

Karlo's House is a region.

City Streets is a region.

A door is usually scenery.

A thing can be examined or unexamined. A thing is usually unexamined.

Before examining a thing (called object):
	Now object is examined.

The Loungeroom is a room. "This is a room you have never seen before, in a house you struggle to remember, on a street you can't name. All of this confusion from a town you only know as Port Kamos. A door is to your north."

The trenchcoat is a player's holdall. The trenchcoat is not openable. It has indefinite article "your". It has description "This trenchcoat gives a stunning balance between hipster and functionality. The pockets seem bottomless." Understand "trench", "coat", "pocket" or "pockets" as the trenchcoat.

After inserting something into the trenchcoat, say "You bury [the noun] in your trenchcoat pocket."

The player is wearing the trenchcoat. 
The rusty iron key is in the Loungeroom. The description of the rusty iron key is "A rusted key, not for a house or a car. Something you would see in a tattoo."

In the loungeroom is a thing called a water bottle. The water bottle can be full, half-empty or empty. Understand "water" as the water bottle. The description of the bottle is "The bottle looks ideal for holding water ([water bottle condition])".

Instead of drinking the water bottle when the water bottle is empty:
	say "Fresh out of water."
	
Instead of drinking the water bottle: if the water bottle is half-empty, now the water bottle is empty; if the water bottle is full, now the water bottle is half-empty; say "Refreshing... The water bottle is now [water bottle condition]."

After printing the name of the water bottle: say " ([water bottle condition])".

The water bottle is full.

In the loungeroom is a TV guide. The description of the TV guide is "An old TV guide. The cover shows a still from that American TV show about the traveling doctors in the Korean War. The title of the TV show completely eludes you, and unfortunately it seems that the name has been ripped cleanly from the cover. How frustrating. Nonetheless, there is nothing worth reading in this magazine."

North of the loungeroom is a beige door. The beige door is a door.

A Humble Garden is north of the beige door. "A well kept garden of aromatic gardenias. The walls around you rise high and are covered in the climbing foliage of the honeysuckle. Through an archway to your west is a children's crossing, and to your east is a rusty iron gate."

East of the Humble Garden is a door called the rusty iron gate. Understand "gate" as the rusty iron gate. The gate is locked and lockable. The rusty iron key unlocks the rusty iron gate. The description of the rusty iron gate is "An old but functional gate. Rustic steel slats run far higher than arms reach. The flaking paint insists that once this gate would have glowed white to match the rendered walls of the garden."

The Well-kept Backyard is east of the rusty iron gate. "The lush buffalo grass doesn't stray a micron in height across the entire yard.  In the northeast corner sits a small shed, and to your west is a rusty iron gate." Northeast of the backyard is a room called Small Shed.

The grass is scenery in the backyard. In the grass is a knife. The description of the knife is "A knife with a repaired wooden handle. This knife has seen some years of use. Blood stains the handle but the blade is clean and polished."

The description of the grass is "[if knife is in grass]A perfectly maintained lawn of Sir Walter Buffalo. A glimmer of light catches your eye in the grass.[otherwise]A perfectly maintained lawn of Sir Walter Buffalo."

Understand "lawn" and "buffalo" as grass.

After taking knife for the first time:
	say "Great find. I can't help but wonder whose blood this is?";
	increase score by 2;

The Loungeroom, Humble Garden, Backyard and Small Shed are in Karlo's House.

The Children's Crossing, A Small Hill and Disintegrated Wharf are in City Streets.

The description of the small shed is "Four square zinc walls prop up a slanted roof. Such a small space, probably not enough room to swing a cat, or hide a corpse, or whatever it is you do in your free time." [make sure the chainsaw is only carried around by hand and then disposed of at some point]
A chainsaw is in the small shed. The description of the chainsaw is "A convenient device, clearly used only once or twice. If one were committed to carrying it around, it would probably make quite the murder weapon..."

The chainsaw is a weapon. The knife is a weapon. The chainsaw is a device. The chainsaw is switched off.

Instead of inserting chainsaw into trenchcoat:
	say "That will hardly fit in there."

Understand "start [something]" as switching [something] on.

Instead of attacking someone with the chainsaw:
	if the chainsaw is switched off:
		say "It is not running. You would just be flailing around a heavy object like a lunatic."

Instead of switching on the chainsaw:
	say "It won't run for very long without any petrol in it, and furthermore, it's a rather impractical weapon anyway. Probably not worth the effort of lugging it around" [make sure Karlo has to have seen the chainsaw to advance, but as soon as he gets it he throws it into the ocean]
	
In the Humble Garden is the honeysuckle. The indefinite article of honeysuckle is "some". The description of the honeysuckle is "A beautiful shrub with a patch of golden flowers. You recall sucking the sweet nectar from the flowers growing at home as a child. There is [honeysuckle condition] left." The honeysuckle is edible.

The honeysuckle can be lots, a bunch, a little or zilch.
Instead of eating the honeysuckle:
	if the honeysuckle is a little, now the honeysuckle is zilch;
	if the honeysuckle is a bunch, now the honeysuckle is a little;
	if the honeysuckle is lots, now the honeysuckle is a bunch;
	say "Delicious, sugary nostalgia. There is now [honeysuckle condition] left."

Instead of eating honeysuckle when honeysuckle is zilch, say "It was greedy, to eat all that. You'll end up with your teeth all grey."

A Children's Crossing is west of the Humble Garden. "From here, you can see the main road extending north and south. There is an abandoned school to your west, and to your east is a humble garden."

West of the Children's Crossing is The Primary School. The description of the Primary School is "A large open field with a horseshoe of demountable buildings making up the local primary school. In the centre of the field is a playground equipped with a rusty slide, and a swing."

North of the Children's Crossing is A Small Hill. The description of the Small Hill is "A nice outpost to survey your surroundings. The street stretches North and South. To your west is a short road. A heavy looking door is to your northwest. You can hear the ocean from here.
[if the bushes are examined]A hidden path leads east through the bushes.[otherwise]To your east are thick and overgrown bushes."

In the small hill are Bushes. The Bushes are scenery. The description of the bushes is "Overgrown salt-bush. You are pretty sure its some kind of massive, obnoxious weed and it looks like someone even had a crack at pulling it out. Actually... it looks like a path follows in through the initial shrubbery!"

Understand "bush" as bushes.

Instead of searching the bushes:
	try examining the bushes.
		
Instead of examining the bushes for the first time:
	Increase score by 3;
	continue the action.

Instead of going East:
	if the player is in the Small Hill:
		if Bushes are unexamined:
			say "There are only bushes to the east.";
		else if Bushes are examined:
			say "Through the bushes is a hidden path to a hidden beach.";
			continue the action;
	otherwise:
		continue the action.
		
After examining the tv guide for the first time:
	now mash is known;

North of the Small Hill is the Middle of the Beach. The description of the middle of the beach is "[if the scene is Awakening]Here you are reminded of brief flashes of last night. You recall stumbling along here after being rushed away from somewhere. To your north is a wharf; a narrow lane to your east; south will take you to a small hill; southwest is the local watering hole; and west is a picnic area."

West of the Small Hill is the Short Road. The description of the short road is "To your north is the entrance to a modest skyscraper. In contrast to the rest of this town, the building is enormous. There are transparent, automatic doors blocking your path in this direction. To your east is a small hill."

North of the Short Road is a door called the Automatic Door. The automatic door is closed.

The description of the automatic door is "A double, sliding, automatic door. The kind you would see at the entrance of a shopping centre or the like. There is a scanner on the door that looks to be in control of who passes."

Instead of opening the automatic door:
	say "It's automatic, and you are existing in front of it. You are trying the best you can! The apparent lack of power in this town must have something to do with the door's noncooperation."

Instead of pulling the automatic door:
	say "It's really more of an automatic door. In any case, the action would be 'slide' since they move laterally."
	
Instead of pushing the automatic door:
	say "It's really more of an automatic door. In any case, the action would be 'slide' since they move laterally."

Instead of attacking the automatic door:
	say "You wouldn't stand a chance!"

A scanner is part of the Automatic Door. The description of the scanner is "A black box likely filled with the nifty electronics, capable of recognising an automatic pass; a fob of some kind or a pass perhaps. It appears to be unpowered, just like everything else you have seen so far. There is probably some fail-safe manual way to get through given a power outage."

A door can be slideable. Doors are usually not slideable.

Sliding is an action applying to one visible thing. Understand "Slide [thing]" as sliding.

The automatic door is slideable.

Instead of sliding a thing:
	if the noun is a slideable door:
		if the noun is closed:
			say "It slides open, rather easily indeed.";
			now the noun is open;
		else if the noun is open:
			say "It slides closed.";	
			now the noun is closed;	
	otherwise:
		say "It won't slide."

North of the Automatic Door is the Main Foyer. The description of the main foyer is "An inviting and ominous room. You feel welcomed in the same way that an old hound feels welcomed at the vet. Even though the air conditioning is not running you still feel far cooler in here than outside. The exit lies to your south. To your west is an elevator shaft with the doors wide open. To your east, the bottom of an emergency stair-well. 

Down the elevator shaft is almost immediate darkness. The pit could be 2 metres deep or 200."

Instead of going west:
	if the player is in the main foyer:
		if the elevator warnings is 0:
			say "You better not. That would almost definitely result in death.";
			now the elevator warnings is 1;
		else if the elevator warnings is 1:
			say "That would end you in a radically suboptimal state.";
			now the elevator warnings is 2;
		else if the elevator warnings is 2:
			say "If you insist.";
			say "You fall only a couple of metres, which would be good if there wasn't a makeshift Punji stake trap down here. You are impaled fatally through almost every major organ. For the few seconds before you die you are actually a little bit impressed at how effecting this death pit was designed.";		
			say "*** You have died ***";
			say paragraph break;
			say "A wanna-be spider-man,";
			say line break;
			say "Still incredibly daft.";
			say "Now a human kebab,";
			say line break;
			say "At the base of a shaft.";
			say paragraph break;
			end the story;
	otherwise:
		continue the action.
			
Knocking on is an action applying to one visible thing. Understand "Knock on [thing]" as knocking on.

Understand "Tap on [thing]" as knocking on.

Instead of knocking on a thing:
	if the noun is a door:
		say "There is no response.";
	otherwise:
		say "You won't likely get an answer."
	
The elevator warnings is a number that varies.

West of the Main Foyer is the Elevator Shaft. The description of the elevator shaft is "I personally have no idea how you got in here without dying. Kudos to you Mr/Mrs person..."

East of the Main Foyer is a room called the Stair Well. The description of the Stair Well is "About 5 sets of stairs going directly up. Not to put words in your mouth but the top floor is the only floor of significance. North is an open door to another room of this resort." [make this less shit]

Up from the Stair Well is the Top Floor. The description of the Top floor is "The top of the stairs. A perfect outpost to watch for predators trying to make their way up. West is an emergency only door."

West of the Top Floor is a door called the emergency door. The emergency door is closed. The description of the emergency door is "An emergency door. Writing printed on a sign reads 'EMERGENCY ONLY. ALARM WILL SOUND IF DOOR IS OPENED'. Underneath written in black crayon is 'Trespassers will be shot'."

Sheen warning is a number that varies.

West of the emergency door is Sheen's Penthouse. The printed name of Sheen's Penthouse is "Penthouse". The description of sheen's penthouse is "A large penthouse with windows looking over the beach. From here you can make out the wharf and the mountain.
[if sheen warning is -1]Sheen stands in front of you, pondering out loud about a new and improved defense system. You overhear through his murmurs 'Lasers! Fire! If only I wasn't petrified of snakes...'"
 
 Sheen is scenery.
 Understand "man" as Sheen.
every turn:
	if the player is in Sheen's Penthouse:
		if sheen warning is 0:
			say "Shaking and pointing a gun at you is a pale man. His voice cracks as he screams at you 'Leave now or I'll blow your brains out!'";
			now sheen warning is 1;
		else if sheen warning is 1:
			say "'DO NOT MAKE ME DO THIS!' The man screams.";
			now sheen warning is 2;
		else if sheen warning is 2:
			say "I'm warning you!' this time more sincerely...";
			now sheen warning is 3;
		else if sheen warning is 3:
			say "'FINE!' he concedes. 'You win! It is just a toy gun.' He throws the plastic pistol on the ground. 'Who are you and how did you get in here?'
			
			'Oh you just came in through the automatic door? But I shut the power off!'
			
			'It just slid open? Well... Probably for the best that you made it up here before one of those monsters did. Now I can reinforce. The name is Sheen, anyway. Pleasure.'";
			now the toy gun is in Sheen's Penthouse;
			now sheen warning is -1;
			increase score by 3;
			now the printed name of Sheen's Penthouse is "Sheen's Penthouse."

Instead of asking Sheen about "monsters":
	if sheen warning is less than 0:
		say "You haven't seen them? They are terrifying. A plague is sweeping through this town. Something no longer human. It has been taking the people and turning them into some kind of dead, angry, hungry rage.";
	otherwise:
		say "He doesn't appear to be in the mood to take questions."

Instead of asking Sheen about "plague":
	if sheen warning is less than 0:
		say "'I have no idea where it started. I just know that it wasn't here yesterday and now it's running the show. I'm amazed you are still alive, just walking the streets down there!'";
	otherwise:
		say "He doesn't appear to be in the mood to take questions."
	
Instead of asking Sheen about "Port Kamos":
	if sheen warning is less than 0:
		say "'This is the town I grew up in. Everyone I know or have known lives or lived here. In the last few years though, the people have been changing. Every single one of them. And you wan't to know the worst thing? It's not just the people that live in this shit-hole; it's everyone! The whole state. The country. Hell, the whole damned planet!' He appears to be looking directly through you now. He is lost in his own thoughts. 'They are destroying the oceans! Filling the atmosphere with the by-products of their greed and glutony. Nobody else could see it. Not even Mash! People everywhere wanted change, and look what they got...'
		
		'Erm... Sorry. I wandered off a bit there. What were you saying?'";
		now mash is known;
	otherwise:
		say "He doesn't appear to be in the mood to take questions."
	
Instead of asking Sheen about "Mash":
	if sheen warning is less than 0:
		if mash is known:
			say "'Mash? Who mentioned Mash? Look... You need to stop digging before you get yourself killed. There are more dangerous things out there than these monsters.'";
		otherwise:
			say "I hope we aren't cheating now, are we?";
	otherwise:
		say "He doesn't appear to be in the mood to take questions."
		
Mash is a thing. Mash can be known or unknown. Mash is unknown.

The description of Sheen is "[if sheen warning is not -1]A pale man, struggling to convince himself that he is a killer.[otherwise]He is lost in his own thoughts. A tinge of regret peers through the side of his eyes."

Sheen is a man. Sheen is in Sheen's Penthouse. Sheen is holding a toy gun. [do more with sheen here]

North of the Stair Well is the Wrecked Room. The description of the wrecked room is "A trashed rumpus room. Bloodied up carpet. Smashed remains of chairs and tables. Graffiti on the walls and a dust silhouette where a TV used to sit."

In the wrecked room is graffiti. The graffiti is scenery. The description of the graffiti is "Many people have tagged the walls of the room. One wall says 'box' whatever that means. Another says 'mash'. It is hard to say if these have been here for a day or a year."

After examining the graffiti for the first time:
	now mash is known;

Southeast of the Wrecked room is a door called the heavy door. Southeast of the heavy door is the Small Hill. The heavy door is closed.

Instead of opening the heavy door:
	if the player is in the wrecked room:
		continue the action;
	otherwise:
		say "It appears to be an exit only. There is no handle on this side and no obvious way to open it."

every turn:
	if the heavy door has been open for 1 turns:
		say "The heavy door slams shut.";
		now the heavy door is closed.

The description of the heavy door is "[if the player is in the small hill]It appears to be an exit only. There is no handle on this side and no obvious way to open it.[otherwise if the player is in the wrecked room]A stainless steel handle and a sign that says loudly 'EXIT ONLY'."

East of the Small Hill is The Hidden Beach. 

The Hidden Beach, the Car Park, the Picnic Area, the Croseus Club and the Narrow Lane are zombied.

Every turn:
	if the player is in a zombied room:
		say "In broad daylight a monster attacks you! It is in the shape of a human but certainly not like any one you have seen before. This is worse than any amalgamation of any disease that your creative mind could fathom. You gag a little in disgust before this creature sinks its jagged teeth into your skull, piercing the flesh of your juicy brain. For a moment you are actually in hysterics. Everything is funny, then all of a sudden everything is terrifying.";
		say paragraph break;
		say "*** You have turned ***";
		say paragraph break;
		say "A novice explorer,";
		say line break;
		say "Just trekking the land.";
		say "But becoming a meal,";
		say line break;
		say "Wasn't something he planned.";
		end the story.

North of the Hidden Beach is The Jagged Cliff-face. [make a thing that makes cliff face unpassable after a few turns because of rocks smashing or even bring the tide into it]

North of the Cliff-face is Crown Beach.

North of Crown Beach is the Climbing Stairway.

North of the Climbing Stairway is The Summit.

North of the Summit is Crimson's Bunker.

[to do:
-make sure Keh puts up spikes coming down from the summit and if you go that way again you die
-make automatic door slide open manually due to electricity being off and sheen being shocked when he finds this out.
-make wrecked room door lock after leaving through it unless you jam the TV guide in there to keep it open
-first scene will be zombie chase after Karlo's house wakeup. They will direct the character to either
-give sheen something from phentons to know that you have been there. This triggers him to leave.
]

Southwest of Crimson's Bunker is the Asylum.
The Asylum is west of the Summit.
The Asylum is northwest of the Climbing Stairway.

West of the Asylum is a lockable, locked door called Huge Steel Gate. The Steel Key unlocks the steel gate. West of the Steel Gate is the Narrow Lane.

West of the narrow lane is the Middle of the Beach.

North of the narrow lane is The Boat Ramp.

Gnarle's House is south of the narrow lane. The description of Gnarle's house is "A conveniently open-plan apartment."
[this needs heaps more detail]

East of the Picnic Area is the Middle of the beach.

South of the Picnic Area is Desolate Shopping Complex. Understand "Shops" as Desolate Shopping Complex.

South of shopping complex is The General Store.

West of the shopping complex is the News Agent.

Southwest of the Middle of the Beach is The Croseus Club.

South of the Children's Crossing is Car Park.

West of Car Park is Phenton's House. [add a thing to make it not called Phenton's house until we know Phenton]

South of Car Park is Box Beach.

Northeast of Box Beach is Rocky Path. North of Rocky Path is Hidden Beach.

West of Box Beach is Paradise Beach.

West of Paradise Beach is Delapedated Shack. The description of the Delapedated Shack is "The shack stands on stilts and seems to sway in the wind. You are at the bottom of the ladder to climb onto the first floor." [make hermit stuff better]

South of Paradise Beach is Sand Spit. [make sure sand spit is only usable in some parts of the day. maybe make it a door]

South of Sand Spit is The Island.

South of The Island is the Lighthouse.

North of the Middle of the Beach is a Disintegrated Wharf.  Understand "wharf" as the Disintegrated Wharf. The description of the wharf is "An old, rotting, timber wharf. Its time ravished hand rail all but collapsed. Planks missing along parts of its length, and a platform that was once connected to the main structure sits about 3 metres off the northern tip. There is an undeniable urge to set foot on that platform."

Before jumping:
	if the player is in the disintegrated wharf:
		say "If you insist...";
		say "*** You have died ***";
		say paragraph break;
		say "An almost hero,";
		say line break;
		say "from a regular chap.";
		say "Died like a dickhead,";
		say line break;
		say "and failed the gap.";
		say paragraph break;
		end the story.

Rule for printing the player's obituary:
	wait for any key;
